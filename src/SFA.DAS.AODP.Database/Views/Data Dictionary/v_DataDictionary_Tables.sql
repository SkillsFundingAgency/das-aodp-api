CREATE View [dbo].[v_DataDictionary_Tables] as
SELECT 
    c.TABLE_SCHEMA AS [Schema],
    c.TABLE_NAME AS [Table Name],
    c.COLUMN_NAME AS [Column Name],
    c.DATA_TYPE AS [Data Type],
    c.CHARACTER_MAXIMUM_LENGTH AS [Max Length],
    c.IS_NULLABLE AS [Is Nullable],
	C.COLUMN_DEFAULT,
    CASE WHEN kcu.CONSTRAINT_NAME IS NOT NULL AND tc.CONSTRAINT_TYPE = 'PRIMARY KEY' THEN 'Primary Key'
         WHEN kcu.CONSTRAINT_NAME IS NOT NULL AND tc.CONSTRAINT_TYPE = 'FOREIGN KEY' THEN 'Foreign Key'
         ELSE NULL END AS [Key Type],
    fkcu.TABLE_SCHEMA AS [Referenced Schema],
    fkcu.TABLE_NAME AS [Referenced Table],
    fkcu.COLUMN_NAME AS [Referenced Column]
FROM 
    INFORMATION_SCHEMA.COLUMNS c
LEFT JOIN 
    INFORMATION_SCHEMA.KEY_COLUMN_USAGE kcu 
    ON c.TABLE_SCHEMA = kcu.TABLE_SCHEMA
    AND c.TABLE_NAME = kcu.TABLE_NAME
    AND c.COLUMN_NAME = kcu.COLUMN_NAME
LEFT JOIN 
    INFORMATION_SCHEMA.TABLE_CONSTRAINTS tc 
    ON kcu.CONSTRAINT_SCHEMA = tc.CONSTRAINT_SCHEMA
    AND kcu.CONSTRAINT_NAME = tc.CONSTRAINT_NAME
LEFT JOIN 
    INFORMATION_SCHEMA.REFERENTIAL_CONSTRAINTS rc 
    ON kcu.CONSTRAINT_SCHEMA = rc.CONSTRAINT_SCHEMA
    AND kcu.CONSTRAINT_NAME = rc.CONSTRAINT_NAME
LEFT JOIN 
    INFORMATION_SCHEMA.KEY_COLUMN_USAGE fkcu 
    ON rc.UNIQUE_CONSTRAINT_SCHEMA = fkcu.CONSTRAINT_SCHEMA
    AND rc.UNIQUE_CONSTRAINT_NAME = fkcu.CONSTRAINT_NAME
WHERE 
    c.TABLE_NAME NOT IN (SELECT TABLE_NAME FROM INFORMATION_SCHEMA.VIEWS)

GO